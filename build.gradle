buildscript {
    apply from: "$rootProject.projectDir/common.gradle"
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://nexus.corp.twilio.com/content/repositories/snapshots/"
        }
        maven {
            url "http://nexus.corp.twilio.com/content/repositories/releases/"
        }
        maven {
            url "https://maven.google.com"
        }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.jakewharton:butterknife-gradle-plugin:8.4.0'
        classpath 'com.google.gms:google-services:3.0.0'
        classpath 'com.google.firebase:firebase-plugins:1.0.5'
        classpath 'de.felixschulze.gradle:gradle-hockeyapp-plugin:3.6'
    }
}

allprojects {
    apply from: "$rootProject.projectDir/common.gradle"
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "http://nexus.corp.twilio.com/content/repositories/snapshots/"
        }
        maven {
            url "http://nexus.corp.twilio.com/content/repositories/releases/"
        }
        maven {
            url "https://maven.google.com"
        }
    }
}

task tagReleaseCandidate {
    description = 'Tag commit as release candidate'
    group = 'Git'

    doLast {
        def rcTag = "${versionMajor}.${versionMinor}.${versionPatch}-rc${versionRc}"
        def rcTagPath = "tags/${rcTag}"
        exec {
            workingDir "${rootDir}"
            commandLine "git", "tag", "-a", "${rcTag}", "-m", "\"${rcTag} release candidate\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "${rcTagPath}"
        }
    }
}

task tagRelease {
    description = 'Tag commit as release'
    group = 'Git'

    doLast {
        def rcTag = "${versionMajor}.${versionMinor}.${versionPatch}-rc${versionRc}"
        def releaseVersion = System.getenv("RELEASE_VERSION")
        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "${rcTag}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git",
                    "tag",
                    "-a",
                    "${releaseVersion}",
                    "-m",
                    "\"${releaseVersion} release\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "${releaseVersion}"
        }
    }
}

task incrementRc {
    description = 'Increment the current release candidate version'
    group = 'Git'

    doLast {
        def gitBranch = System.getenv("GIT_BRANCH")
        def nextVersionRc = "${versionRc + 1}"
        // Create build directory if not created
        if (!buildDir.exists()) {
            buildDir.mkdir()
        }
        def tmpProjectPropertiesFile = new FileOutputStream("${buildDir}/gradle.properties")
        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "${gitBranch}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "echo", "Incrementing RC from ${versionRc} to ${nextVersionRc}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "sed",
                    "s@versionRc=.*@versionRc=${nextVersionRc}@",
                    "gradle.properties"
            standardOutput tmpProjectPropertiesFile
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "${buildDir}/gradle.properties", "gradle.properties"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "commit", "gradle.properties", "-m", "\"Bump RC version\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "${gitBranch}"
        }
    }
}

task incrementVersionAfterRelease {
    description = 'Increment the SDK version after a release'
    group = 'Git'

    doLast {
        def gitBranch = System.getenv("GIT_BRANCH")
        def nextVersionPatch = versionPatch + 1
        // Create build directory if not created
        if (!buildDir.exists()) {
            buildDir.mkdir()
        }
        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "${gitBranch}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "echo", "Incrementing from versionPatch ${versionPatch} to " +
                    "${nextVersionPatch}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "sed",
                    "s@versionPatch=.*@versionPatch=${nextVersionPatch}@",
                    "gradle.properties"
            standardOutput new FileOutputStream("${buildDir}/gradle.properties")
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "${buildDir}/gradle.properties", "gradle.properties"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "echo", "Resetting versionRc to 1"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "sed",
                    "s@versionRc=.*@versionRc=1@",
                    "gradle.properties"
            standardOutput new FileOutputStream("${buildDir}/gradle.properties")
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "${buildDir}/gradle.properties", "gradle.properties"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "echo", "Resetting phaseNumber to 1"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "sed",
                    "s@phaseNumber=.*@phaseNumber=1@",
                    "gradle.properties"
            standardOutput new FileOutputStream("${buildDir}/gradle.properties")
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "${buildDir}/gradle.properties", "gradle.properties"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "commit", "gradle.properties", "-m", "\"Bump patch and reset rc version\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "${gitBranch}"
        }
    }
}

task incrementVersionAfterPreRelease {
    description = 'Increment the SDK version after a pre-release'
    group = 'Git'

    doLast {
        def gitBranch = System.getenv("GIT_BRANCH")
        def nextPhaseNumber = phaseNumber + 1
        // Create build directory if not created
        if (!buildDir.exists()) {
            buildDir.mkdir()
        }
        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "${gitBranch}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "echo", "Incrementing from phaseNumber ${phaseNumber} to " +
                    "${nextPhaseNumber}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "sed",
                    "s@phaseNumber=.*@phaseNumber=${nextPhaseNumber}@",
                    "gradle.properties"
            standardOutput new FileOutputStream("${buildDir}/gradle.properties")
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "${buildDir}/gradle.properties", "gradle.properties"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "commit", "gradle.properties", "-m", "\"Bump phase number\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "${gitBranch}"
        }
    }
}

task incrementVersion(type: GradleBuild) {
    description = 'Increment the SDK version'
    group = 'Git'
    def releaseVersion = System.getenv("RELEASE_VERSION") == null ?
            ("") :
            (System.getenv("RELEASE_VERSION"))
    def incrementVersionTask = releaseVersion.contains("preview") ||
            releaseVersion.contains("beta") ?
            ('incrementVersionAfterPreRelease') :
            ('incrementVersionAfterRelease')
    tasks = [incrementVersionTask]
}

task checkoutRelease(type: Exec) {
    description = 'Checkout tagged release'
    group = 'Git'
    def releaseVersion = System.getenv("RELEASE_VERSION")
    commandLine "git", "checkout", "${releaseVersion}"
}

task publishReleaseCandidateArtifacts {
    description = 'Publish Video Android AAR and javadocs to stage CDN'
    group = 'Publishing'
    dependsOn 'library:createJavadocs'

    doLast {
        // Clean up any previous rc
        exec {
            workingDir "${rootDir}"
            commandLine "rm", "-rf", "target", "||", "true"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "rm", "-rf", "dist", "||", "true"
        }

        // Copy artifacts where sdk release tool expects them
        exec {
            workingDir "${rootDir}"
            commandLine "mkdir", "target"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "cp",
                    "library/build/outputs/aar/library-release.aar",
                    "target/twilio-video-android.aar"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "cp", "-r", "library/build/docs/javadoc", "target/"
        }

        // Perform rc with sdk release tool
        exec {
            workingDir "${rootDir}"
            commandLine "./scripts/rc-release-script.sh"
        }
    }
}

task publishLibraryJavadocs {
    description = 'Publish Video Android Javadocs to gh-pages branch'
    group = 'Publishing'
    dependsOn 'checkoutRelease'
    dependsOn 'library:createJavadocs'
    def gitBranch = System.getenv("GIT_BRANCH")
    def releaseVersion = System.getenv("RELEASE_VERSION") == null ?
            ("") :
            (System.getenv("RELEASE_VERSION"))
    boolean preRelease = releaseVersion.contains("preview") || releaseVersion.contains("beta")
    def pinLatestDocsCommand = preRelease ?
            // Pin command is a no-op for pre releases
            (["true"]) :
            (["ln", "-sfn", "${releaseVersion}", "docs/latest"])

    doLast {
        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "gh-pages"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "mv", "library/build/docs/javadoc", "docs/${releaseVersion}"
        }

        exec {
            workingDir "${rootDir}"
            commandLine pinLatestDocsCommand
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "add", "docs/${releaseVersion}", "docs/latest"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "commit", "-m", "\"${releaseVersion} release docs\""
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "push", "origin", "gh-pages"
        }

        exec {
            workingDir "${rootDir}"
            commandLine "git", "checkout", "${gitBranch}"
        }
    }
}

task promoteReleaseCandidateToRelease(type: Exec) {
    description = 'Promote Video Android AAR and javadocs from stage to prod CDN'
    group = 'Publishing'
    def rcVersion = "${versionMajor}.${versionMinor}.${versionPatch}-rc${versionRc}"
    environment "RC_BUILD_NUMBER", "${versionRc}"
    environment "SDK_RC_VERSION", "${rcVersion}"
    commandLine "./scripts/promote-rc-to-release.sh"
}

task publishReleaseArtifacts(type: Exec) {
    description = 'Publish Video Android AAR and javadocs prod CDN'
    group = 'Publishing'
    dependsOn checkoutRelease
    dependsOn promoteReleaseCandidateToRelease
    def rcVersion = "${versionMajor}.${versionMinor}.${versionPatch}-rc${versionRc}"
    def releaseVersion = System.getenv("RELEASE_VERSION") == null ?
            ("") :
            (System.getenv("RELEASE_VERSION"))
    // Do not pin pre releases
    def releaseCommand = releaseVersion.contains("preview") || releaseVersion.contains("beta") ?
            ("true") :
            ("./scripts/pin-release.sh")
    environment "RC_BUILD_NUMBER", "${versionRc}"
    environment "SDK_RC_VERSION", "${rcVersion}"

    commandLine releaseCommand
}

task firebaseTestLabCheckLibrary(type: Exec) {
    description = 'Runs library instrumentation tests on Firebase Test Lab'
    group = 'Test'
    dependsOn 'library:assembleDebugAndroidTest'
    dependsOn 'app:assembleDebug'
    commandLine 'gcloud',
            'firebase',
            'test',
            'android',
            'run',
            '--project',
            'video-app-79418',
            'video-app-args.yaml:library-instrumentation-tests'
}

task firebaseTestLabCheckLibraryCamera(type: Exec) {
    description = 'Runs library instrumentation tests that use camera on Firebase Test Lab'
    group = 'Test'
    dependsOn 'library:assembleDebugAndroidTest'
    dependsOn 'app:assembleDebug'
    commandLine 'gcloud',
            'firebase',
            'test',
            'android',
            'run',
            '--project',
            'video-app-79418',
            'video-app-args.yaml:library-camera-tests'
}

task checkLibrary(type: GradleBuild) {
    description = 'Runs all tests and checks for the library'
    group = 'Test'
    tasks = [
            'clean',
            'assemble',
            'library:testDebugUnitTest',
            'firebaseTestLabCheckLibrary',
            'firebaseTestLabCheckLibraryCamera'
    ]
}

task nexusLibrarySnapshotUpload(type: GradleBuild) {
    description = 'Publish Video Android AAR snapshot to Nexus'
    group = 'Publishing'
    dependsOn checkLibrary
    buildFile = file('library/build.gradle')
    tasks = ['uploadArchives']
    startParameter.projectProperties += gradle.startParameter.projectProperties + [
            'snapshot': true,
            'maven.repo': 'https://nexus.corp.twilio.com/content/repositories/snapshots',
            'maven.username': null,
            'maven.password': null
    ]
}

task bintrayLibraryReleaseCandidateUpload(type: GradleBuild) {
    description = 'Publish Video Android AAR release candidate to internal Bintray'
    group = 'Publishing'
    buildFile = file('library/build.gradle')
    tasks = ['clean', 'assemble', 'uploadArchives']
    startParameter.projectProperties += gradle.startParameter.projectProperties + [
            'releaseCandidate': true,
            'maven.repo': 'https://api.bintray.com/maven/twilio/internal-releases/video-android/;publish=1',
            'maven.username': "${getBintrayUsername()}",
            'maven.password': "${getBintrayPassword()}"
    ]
}

task bintrayLibraryReleaseUpload(type: GradleBuild) {
    description = 'Publish Video Android AAR to release Bintray'
    group = 'Publishing'
    dependsOn checkoutRelease
    buildFile = file('library/build.gradle')
    tasks = ['clean', 'assemble', 'uploadArchives']
    def releaseVersion = System.getenv("RELEASE_VERSION") == null ?
            ("") :
            (System.getenv("RELEASE_VERSION"))
    boolean preRelease = releaseVersion.contains("preview") || releaseVersion.contains("beta")
    startParameter.projectProperties += gradle.startParameter.projectProperties + [
            'preRelease': preRelease,
            'maven.repo': 'https://api.bintray.com/maven/twilio/releases/video-android/;publish=1',
            'maven.username': "${getBintrayUsername()}",
            'maven.password': "${getBintrayPassword()}"
    ]
}

task nexusLibraryReleaseCandidateUpload(type: GradleBuild) {
    description = 'Publish Video Android AAR release candidate to Nexus'
    group = 'Publishing'
    buildFile = file('library/build.gradle')
    tasks = ['clean', 'assemble', 'uploadArchives']
    startParameter.projectProperties += gradle.startParameter.projectProperties + [
            'releaseCandidate': true,
            'maven.repo': 'https://nexus.corp.twilio.com/content/repositories/releases',
            'maven.username': null,
            'maven.password': null
    ]
}
