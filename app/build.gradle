apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-crash'
apply plugin: 'com.jakewharton.butterknife'

android {
    compileSdkVersion 26
    buildToolsVersion "26.0.1"

    packagingOptions {
        exclude 'META-INF/LICENSE'
    }

    defaultConfig {
        applicationId "com.twilio.video.app"

        minSdkVersion 16
        targetSdkVersion 26

        versionName generateVersionName()
        versionCode generateVersionCode()

        buildConfigField 'String', 'HOCKEY_APP_ID', "${getHockeyAppId()}"

        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    lintOptions {
        disable 'BinaryOperationInTimber'
    }


    /**
     * Use the debug keystore to sign all variants of the test app
     * since this is only used internally.
     */
    signingConfigs {
        release {
            storeFile file(keystore)
            storePassword keystorePassword
            keyAlias releaseKeyAlias
            keyPassword releaseKeyPassword
        }
        debug {
            storeFile file('video-android-app.keystore')
            storePassword 'android'
            keyAlias 'debug'
            keyPassword 'android'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles.add(file('android-proguard-rules.pro'))
            proguardFiles.add(file("${project(':library').projectDir}/proguard-rules.pro"))
            proguardFiles.add(file("proguard-rules.pro"))
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
            ndk {
                debuggable true
            }
            signingConfig signingConfigs.debug
        }
    }

    productFlavors {
        internal {
            applicationId "com.twilio.video.app.internal"
            buildConfigField 'String', 'TOPOLOGY_DEFAULT', '"P2P"'
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"prod"'
        }

        production {
            buildConfigField 'String', 'TOPOLOGY_DEFAULT', '"SFU"'
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"prod"'
        }

        development {
            applicationId "com.twilio.video.app.development"
            buildConfigField 'String', 'TOPOLOGY_DEFAULT', '"P2P"'
            buildConfigField 'String', 'ENVIRONMENT_DEFAULT', '"prod"'
            buildConfigField("java.util.Map<String, String>",
                    "twilioCredentials",
                    "${getTwilioVideoAppConfig()}")
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name

        // Ignore production debug and development release builds
        if ((names.contains("production") && variant.buildType.name == "debug") ||
                (names.contains("development") && variant.buildType.name == "release")) {
            setIgnore(true)
        }
    }
}

dependencies {
    compile project(':library')
    compile project(':twilioapi')
    compile project(':env')
    compile 'net.hockeyapp.android:HockeySDK:3.5.0'
    compile 'com.android.support:design:26.1.0'
    compile 'com.android.support:preference-v14:26.1.0'
    compile 'com.jakewharton.timber:timber:4.1.0'
    compile 'com.appyvet:materialrangebar:1.3'
    compile 'com.jakewharton:butterknife:8.4.0'
    compile 'com.google.guava:guava:19.0'
    compile 'com.android.support:support-v4:26.1.0'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.firebase:firebase-crash:10.0.1'
    compile 'com.google.firebase:firebase-auth:10.0.1'
    compile 'com.google.android.gms:play-services-auth:10.0.1'

    compile 'com.google.dagger:dagger:2.10'
    compile 'com.google.dagger:dagger-android:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'

    compile "io.reactivex.rxjava2:rxjava:2.0.6"
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.1.0'

    compile 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    developmentCompile project(':token')
}

def getHockeyAppId() {
    def hockeyAppId = "\"\""

    if (project.hasProperty("hockeyAppId")) {
        hockeyAppId = "\"${project.property("hockeyAppId")}\""
        logger.log(LogLevel.INFO, "HockeyApp ID: ${hockeyAppId}")
    } else {
        logger.log(LogLevel.INFO, "No HockeyApp ID provided")
    }

    return hockeyAppId
}

def getTwilioVideoAppConfig() {
    if (project.file('twilio-video-app.json').exists()) {
        def slurper = new groovy.json.JsonSlurper()
        def twilioVideoConfig = slurper.parse(project.file('twilio-video-app.json'));

        // Validate that mandatory credentials are set
        assertCredentialDefined(twilioVideoConfig.credentials, "account_sid");
        assertCredentialDefined(twilioVideoConfig.credentials, "api_key");
        assertCredentialDefined(twilioVideoConfig.credentials, "api_key_secret");
        assertCredentialDefined(twilioVideoConfig.credentials, "configuration_profile_sid");

        // Generate credential HashMap
        def hashMapPrefix = "new java.util.HashMap<String , String>() {{\n";
        def hashMapSuffix = "}}";
        def hashMap = hashMapPrefix;

        twilioVideoConfig.credentials.each { credentialsEntry ->
            def hashMapEntry = "    put(\"${credentialsEntry.key}\", " +
                    "\"${credentialsEntry.value}\");\n";
            hashMap += hashMapEntry
        }

        return hashMap + hashMapSuffix;
    } else {
        logger.log(LogLevel.WARN, "Could not find twilio-video-app.json in " +
                project.projectDir.toString() + ". Development variant cannot connect to room" +
                " without credentials")
    }
}

def assertCredentialDefined(credentials, credentialKey) {
    assert credentials.containsKey(credentialKey) :
            "twilio-video-app.json must contain key " + credentialKey;
    assert credentials.get(credentialKey) != null : credentialKey + " must not be null";
    assert !credentials.get(credentialKey).equals("") : credentialKey + " must not be empty";
}

apply plugin: 'com.google.gms.google-services'
